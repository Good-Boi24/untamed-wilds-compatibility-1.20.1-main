plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.10-SNAPSHOT" apply false
    id "com.github.johnrengelman.shadow" version "8.1.1" apply false
    id "com.modrinth.minotaur" version "2.+" apply false
    id "net.darkhax.curseforgegradle" version "1.1.25" apply false
}

architectury {
    minecraft = minecraft_version
}

allprojects {
    version = mod_version
    group = maven_group
}

subprojects {
    apply plugin: "java"
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = archives_base_name
    }

    java.toolchain.languageVersion.set(JavaLanguageVersion.of(java_version))

    repositories {
        maven {
            name = "ParchmentMC"
            url = "https://maven.parchmentmc.org"
        }

        maven {
            url = "https://cursemaven.com"
            content {
                includeGroup("curse.maven")
            }
        }
        mavenLocal()
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup("maven.modrinth")
            }
        }
        maven { url "https://maven.ladysnake.org/releases" }
        maven {
            name = 'GeckoLib'
            url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
            content {
                includeGroupByRegex("software\\.bernie.*")
                includeGroup("com.eliotlash.mclib")
            }
        }
        maven { url "https://dl.cloudsmith.io/public/darkpred-mods/more-hitboxes/maven/" }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.minecraftforge.net/" }
        maven { url "https://modmaven.dev/" }
        maven { url "https://dvs1.progwml6.com/files/maven/" }//JEI
        maven {
            name = "tterrag maven"
            url =  "https://maven.tterrag.com/"
        }
        maven { url "https://maven.createmod.net" } // Create, Ponder, Flywheel
    }

    dependencies {
        minecraft "com.mojang:minecraft:$minecraft_version"
    }

    loom {
        silentMojangMappingsLicense()
    }

    java {
        //withSourcesJar()

        sourceCompatibility = JavaVersion.valueOf("VERSION_$java_version")
        targetCompatibility = JavaVersion.valueOf("VERSION_$java_version")
    }

    // Include resources generated by data generators.
    sourceSets {
        main {
            resources.srcDir file("src/generated/resources")
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set(java_version.toInteger())
    }



    tasks {
        def expandProps = [
                "mod_version"                  : mod_version,
                "mod_name"                     : mod_name,
                "mod_id"                       : mod_id,
                "mod_license"                  : mod_license,
                "mod_description"              : mod_description,
                "group"                        : project.group, //Else we target the task's group.
                "minecraft_version"            : minecraft_version,
                "minecraft_version_range_lower": minecraft_version_range_lower,
                "minecraft_version_range_upper": minecraft_version_range_upper,
                "fabric_version"               : fabric_api_version,
                "fabric_loader_version"        : fabric_loader_version,
                "fabric_loader_version_range"  : fabric_loader_version_range,
                "fabric_mod_authors"           : mod_authors.split(", ").collect { "\"" + it + "\"" },
                "forge_version"                : forge_version,
                "forge_version_range"          : forge_version_range,
                "forge_loader_version_range"   : forge_loader_version_range,
                "forge_mod_authors"            : mod_authors,
                "java_version"                 : java_version
        ]

        processResources {
            inputs.properties(expandProps)
            filesMatching(["META-INF/mods.toml", "pack.mcmeta", "fabric.mod.json"]) {
                expand(expandProps)
            }
        }

        jar {
            archiveBaseName.set("${archives_name}-${project.name}-${minecraft_version}")
            manifest {
                attributes([
                        "Specification-Title"   : mod_name,
                        "Specification-Vendor"  : mod_team,
                        "Specification-Version" : archiveVersion,
                        "Implementation-Title"  : project.name,
                        "Implementation-Version": archiveVersion,
                        "Implementation-Vendor" : mod_team
                ])
            }
        }
    }
}
