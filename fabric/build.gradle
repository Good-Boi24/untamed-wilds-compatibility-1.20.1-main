import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id "com.github.johnrengelman.shadow"
    id "com.modrinth.minotaur"
    id "net.darkhax.curseforgegradle"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-$parchment_mc_version:$parchment_date@zip")
    }

    //Required
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
    modApi "net.fabricmc.fabric-api:fabric-api:$fabric_api_version+$minecraft_version"
    modApi "dev.architectury:architectury-fabric:$architectury_version"
    modApi include("teamreborn:energy:$energy_version")
    modImplementation include("maven.modrinth:sructurized-reborn:1.20.1-01")//Structurized Reborneborn
    modImplementation "software.bernie.geckolib:geckolib-fabric-$minecraft_version:$gecko_lib_version"
    implementation "com.eliotlash.mclib:mclib:20"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:$minecraft_version-$terra_blender_version"
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:$cardinal_components_version")
    modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:$cardinal_components_version")
    modImplementation include("maven.modrinth:midnightlib:$midnight_lib_version")
    modImplementation "com.github.darkpred.morehitboxes:morehitboxes-fabric-$minecraft_version:$more_hitboxes_version"

    //Optional
    modCompileOnly "maven.modrinth:jade:$jade_version_fabric"
    modRuntimeOnly "maven.modrinth:jade:$jade_version_fabric"
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    modCompileOnly "mezz.jei:jei-$minecraft_version-fabric-api:$jei_version"
    //modRuntimeOnly "mezz.jei:jei-$minecraft_version-fabric:$jei_version"
    modCompileOnly "maven.modrinth:carry-on:$carry_on_version_fabric"
    modCompileOnly "maven.modrinth:farmers-delight-refabricated:$minecraft_version-$farmers_delight_version_fabric"

    //FD addons
    modCompileOnly "maven.modrinth:oceans-delight:fdrf-fabric-1.0.2-1.20"
    modCompileOnly "curse.maven:nethers-delight-fabric-701831:4778206" //1.20.1-1.0.0
    modCompileOnly "maven.modrinth:enders-delight:1.20.1-1.0.2.refabricated"
    modCompileOnly "maven.modrinth:ends-delight:refabricated-1.20.1-2.5"
    modCompileOnly "maven.modrinth:cultural-delights-fabric:1.0.5+1.20.1"
    modCompileOnly "maven.modrinth:pineapple-delight:x85UwMri" //1.20.1-1.0.13
    modCompileOnly "maven.modrinth:festive-delight:Gs9jmquQ" //1.20.1-1.1.0
    modCompileOnly "maven.modrinth:casualness-delight:1.20.1-0.4-FDR"
    modCompileOnly "maven.modrinth:seed-delight:2MhNzl7f" //1.20.1-1.0.1
    modCompileOnly "maven.modrinth:brewin-and-chewin-fabric:3.0.6+1.20.1"
    modCompileOnly "curse.maven:farmers-respite-fabric-811003:5252387" //1.20.1-3.0.2
    modCompileOnly "curse.maven:expanded-delight-620770:5708081" //1.20.1-0.3.2

    //Dev only

   // modLocalRuntime("maven.modrinth:tectonic:3.0.13")
   // modLocalRuntime("maven.modrinth:lithostitched:1.4.11-fabric-1.20")
    modLocalRuntime "maven.modrinth:auth-me:7.0.2+1.20"
    modLocalRuntime "maven.modrinth:huge-structure-blocks:1.0.9"
    modLocalRuntime "curse.maven:worldedit-225608:4586218" //1.20.1
    modLocalRuntime "curse.maven:cyanide-543444:4584673"
    modLocalRuntime "curse.maven:commandstructures-565119:4711623"
    modLocalRuntime "maven.modrinth:betterf3:7.0.2"
    modLocalRuntime "maven.modrinth:lazydfu:0.1.3"
    //modLocalRuntime "maven.modrinth:sodium:mc1.20.1-0.5.13-fabric"
    //modLocalRuntime "maven.modrinth:indium:1.0.36+mc1.20.1"
    //runtimeOnly "org.joml:joml:1.10.4"
    modLocalRuntime "maven.modrinth:modmenu:7.2.2"
    modLocalRuntime "maven.modrinth:camera-utils:fabric-1.20.1-1.0.14"
    //modLocalRuntime "maven.modrinth:replaymod:1.19.2-2.6.20"
    //modLocalRuntime "maven.modrinth:energized-power:1.20.1-2.14.0-fabric" //Energized Power
    modLocalRuntime "maven.modrinth:debugutils:1.20.1-1.0.5"
    //modLocalRuntime "com.github.darkpred.extended_structure_blocks:extended-structure-blocks-fabric:${minecraftVersion}-0.4.0"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}
shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    archiveAppendix = "fabric-$minecraft_version"
}

tasks.named("modrinth") {
    modrinth {
        token = "${project.property("MODRINTH_TOKEN") ?: "no value"}"
        projectId = "IJY7IqPP"
        versionNumber = "$minecraft_version-$mod_version-${project.name}"
        versionType = "release"
        uploadFile = tasks.remapJar
        versionName = "$mod_version for Fabric $minecraft_version"
        debugMode = true
        dependencies {
            required.project("fabric-api")
            required.project("architectury-api")
            required.project("geckolib")
            required.project("terrablender")
            required.project("more-hitboxes")
            embedded.project("cardinal-components-api")
            embedded.project("sructurized-reborn")
            embedded.project("midnightlib")
        }
        changelog = rootProject.file("CHANGELOG.md").text
    }
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group = "publishing"
    description = "Publishes jar to CurseForge"
    apiToken = project.property("CURSEFORGE_TOKEN") ?: "no value"
    debugMode = true
    def mainFile = upload(223908, tasks.remapJar)
    mainFile.addEnvironment("Fabric")
    mainFile.displayName = "$mod_version for Fabric $minecraft_version"
    mainFile.changelog = rootProject.file("CHANGELOG.md").text
    mainFile.changelogType = "markdown"
    mainFile.releaseType = "release"
    mainFile.addRequirement("fabric-api", "architectury-api", "geckolib", "terrablender-fabric", "more-hitboxes")
    mainFile.addEmbedded("cardinal-components-api", "midnightlib")
}

tasks.named("publish") {
    finalizedBy("modrinth", "publishCurseForge")
}
